apply plugin: 'groovy'
apply plugin: 'gradle-one-jar'
apply plugin: 'application'

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = version()

mainClassName = 'net.snet.ledger.LedgerPlService'

project.ext {
  dropwizardVersion = '0.6.2'
  serviceConfig = 'ledger-pl-service-dev.yml'
}

dependencies {
  compile 'com.yammer.dropwizard:dropwizard-core:' + dropwizardVersion
	compile 'com.yammer.dropwizard:dropwizard-client:' + dropwizardVersion
  compile fileTree(dir: 'src/main/vbs', include: '*.vbs')
  testCompile 'org.codehaus.groovy:groovy-all:1.8.6'
	testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
	testCompile 'cglib:cglib-nodep:3.1'
	testCompile 'org.objenesis:objenesis:2.1'
}

repositories {
  mavenCentral()
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.9'
}

run {
  args 'server', './src/dist/config/' + serviceConfig
}

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.github.rholder:gradle-one-jar:1.0.3'
  }
}

task oneJar(type: OneJar) {
  mainClass = mainClassName
}

artifacts {
  oneJar
}

startScripts {
  optsEnvironmentVar = 'APP_HOME_PROP'
  doLast {
    def windowsScriptFile = file getWindowsScript()
    windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME_PROP%', '-Ddw.appHome=%APP_HOME%')
    def unixScriptFile    = file getUnixScript()
    unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME_PROP', '-Ddw.appHome=$APP_HOME')
  }
}

task stage(dependsOn: ['clean', 'oneJar'])

def version() {
  def version = new File("$projectDir/VERSION.txt").text.trim()
  if (!project.hasProperty('release')) {
    version +=  '+' + String.format('%tY%<tm%<td.%<tH%<tM%<tS', new Date())
  }
  version
}
