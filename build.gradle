apply plugin: 'groovy'
apply plugin: 'gradle-one-jar'
apply plugin: 'application'

sourceCompatibility = 1.6
targetCompatibility = 1.6

version = version()

mainClassName = 'net.snet.ledger.LedgerPlService'

// IntelliJ IDEA default gradle test folder interference fix
test.exclude("$name/**")

project.ext {
  dropwizardVersion = '0.7.1'
  serviceConfig = 'ledger-pl-service-dev.yml'
}

dependencies {
  compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
  compile 'io.dropwizard:dropwizard-configuration:' + dropwizardVersion
	compile 'io.dropwizard:dropwizard-client:' + dropwizardVersion
  compile fileTree(dir: 'src/main/vbs', include: '*.vbs')
	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
	testCompile 'cglib:cglib-nodep:3.1'
	testCompile 'org.objenesis:objenesis:2.1'
}

repositories {
  mavenCentral()
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.9'
}

run {
  args 'server', './src/dist/config/' + serviceConfig
}

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.github.rholder:gradle-one-jar:1.0.3'
  }
}

task oneJar(type: OneJar) {
  mainClass = mainClassName
}

artifacts {
  oneJar
}

startScripts {
  optsEnvironmentVar = 'APP_HOME_PROP'
  doLast {
    def windowsScriptFile = file getWindowsScript()
    windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME_PROP%', '-Ddw.appHome=%APP_HOME%')
    def unixScriptFile    = file getUnixScript()
    unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME_PROP', '-Ddw.appHome=$APP_HOME')
  }
}

task stage(dependsOn: ['clean', 'oneJar'])

def version() {
  def version = new File("$projectDir/VERSION.txt").text.trim()
  if (!project.hasProperty('release')) {
    version +=  '+' + String.format('%tY%<tm%<td.%<tH%<tM%<tS', new Date())
  }
  version
}
